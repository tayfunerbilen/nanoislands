func nb-user(nodeset user) {
    default = {
        'size': 'm'
    }

    nb-block('user', nb-extend(default, user))
}

match .user nb {

    <span data-nb="user">
          _size-theme-type-class({
                'name': 'user'
                'size': .size
                if .justify {
                    'type': .justify
                }
            })
            @class += " _init"
        apply . nb-main-attrs


        if !.rightToLeft {
            @class += ' nb-ltr-user'
            apply . nb-user-pic
            apply . nb-user-name
        } else {
            @class += ' nb-rtl-user'
            apply . nb-user-name
            apply . nb-user-pic
        }
    </span>
}

match .user nb-user-pic {
    <span >
        _size-theme-type-class({
            'name': 'user-avatar'
            'size': .size
        })

        if .userpic {
            @style+= " background-image: url('{.userpic}')"
        }

        if .notices > 0 {
            <span class="nb-user__notice-count">
                .notices
            </span>
        }
    </span>
}

match .user nb-user-name {
    <span class="nb-user-label">
        <span class="nb-user-name _link" title="{.username}">
            .username
        </span>
        if .email {
            <span class="nb-user-email" title="{.email}">
               .email
            </span>
        }
    </span>
}
