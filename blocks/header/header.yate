func nb-header(nodeset header) {
    default = {
        'arrow': {
            'search': 'true'
            'buttonContent': 'Найти'
            'class': '_nb-header-arrow'
        }
        'services': 'true'
        'settings': 'true'
        'yaHref': 'http://yandex.ru'
    }

    _nb-block('header', _nb-deep-extend(default, header))
}

match .header nb {
    <div class="nb-header _init" data-nb="header">
        apply . nb-main-attrs
        <div class="_nb-header-actions">

            if .settings == 'true' {

                <div class="_nb-header-button">
                    <div class="_nb-header-button-icon">
                        nb-icon({
                            'icon': 'settings'
                            'size': 'm'
                        })
                    </div>
                </div>
            }

            if .services == 'true' {

                <div class="_nb-header-button">
                    <div class="_nb-header-button-icon">
                        nb-icon({
                            'icon': 'services'
                            'size': 'm'
                        })
                    </div>
                </div>
            }
            if .user {
                nb-user(.user)
            }
        </div>
        <div class="_nb-header-main">
            <a href="{.yaHref}" class="_nb-header-logo"></a>
            nb-arrow(.arrow)
        </div>
    </div>
}