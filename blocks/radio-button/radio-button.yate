func nb-radio-button(nodeset options) {
    default = {
        'size': 'm'
        'theme': 'normal'
        'type': 'radio'
    }

    nb-block('radio-button', nb-extend(default, options))
}

match .radio-button nb {
    <div>
         _size-theme-type-class({
            'name': 'radio-button'
            'size': .size
            'theme': .theme
         })

        @class += " nb-group"

        apply . nb-main-attrs
        for .group {

            uniq = 'nb-radio-button_{nb-uniq()}'
            <span class="nb-radio-button-button nb-complex-group-item">
                <input class="nb-radio-button-radio" type="radio" name="{ ..name }" value="{ .value }" id="{ uniq }">
                    if .checked {
                        @checked = 'checked'
                    }
                    if .disabled {
                        @disabled = 'disabled'
                    }

                    if .name {
                        @name = .name
                    }

                    if .attrs.name {
                        @name = .attrs.name
                    }
                </input>

                nb-button(nb-extend(., {
                    'class': [
                        'nb-complex-group-item-subject'
                        'js-button'
                    ]
                    'type': 'label'
                    'static': true()
                    'id': false()
                    'attrs': {
                        'for': uniq
                        'data-value': .value
                    }
                }))

            </span>

        }

    </div>
}
